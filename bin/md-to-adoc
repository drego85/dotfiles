#!/usr/bin/env -S gawk -f
#
# This is a small Gnu's AWK script to convert Markdown text files to Asciidoctor
# format. It can converts only a really small Markdown's functions, because it
# is tailored to my needs.
#
# I use this script to convert dreams and journal entries to Asciidoctor. I
# write dreams and some journal entries in Markdown because I user Markor on
# Android, a open source application that has only Markdown support.
#
# It converts every file that ends with ".md" suffix and write the result to
# the same file but with ".adoc" suffix. The original file is not touched.
#
# To learn more about Asciidoctor: https://asciidoctor.org
#
# Copyright (C) 2020 Emanuele Petriglia <inbox@emanuelepetriglia.com>
# All right reserved. This file is licensed under the MIT License.

BEGIN {
    if (ARGC == 1) {
        print "Error: no files given!"
        exit 1
    }
}

BEGINFILE {
    if ((filename_err = sub(/\.md$/, ".adoc", FILENAME)) == 0) {
        print "Error: " FILENAME " is not a Markdown text file! Skipping..."
        nextfile
    }
}

# Use Asciidoctor's header instead of Markdowns's with a bottom bar.
FNR == 1 {
    print "= " $0 > FILENAME

    next
}

# Replace the bottom bar with custom Asciidoctor attributes.
FNR == 2 {
    print ":hardbreaks:" > FILENAME

    next
}

# Remove the two last spaces, because I use ":hardbreaks:" attribute.
{
    sub(/\s\s$/, "", $0)

    print $0 > FILENAME
}

ENDFILE {
    if (filename_err != 0)
        close(FILENAME)
}
